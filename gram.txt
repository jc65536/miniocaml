expr ::=
    | value-name                                variables
    | constant                                  constants
    | "(" expr ")"                              grouping
    | expr { "," expr }+                        tuples
    | expr "::" expr                            list cons
    | "[" expr { ";" expr } "]"                 lists
    | expr { expr }+                            function application
    | expr infix-op expr                        infix ops
    | "if" expr "then" expr "else" expr         if-else statements
    | "match" expr "with" pattern-matching      match statements
    | "fun" pattern "->" expr                   lambdas
    | "let" [ "rec" ] let-binding "in" expr     let bindings

infix-op ::= "+" | "*" | "=" | "<" | "||" | "&&"

constant ::= integer-literal | "false" | "true" | "()" | "[]"

pattern-matching ::= { "|" pattern "->" expr }+

pattern ::=
    | value-name
    | "_"
    | constant
    | "(" pattern ")"
    | pattern "|" pattern
    | pattern { "," pattern }+
    | "[" pattern { ";" pattern } "]"
    | pattern "::" pattern

let-binding ::= pattern "=" expr

---

expr ::=
    | value-name                                variables
    | constant                                  constants
    | "(" expr ")"                              grouping
    | expr expr-tuple-rep                       tuples
    | expr "::" expr                            list cons
    | "[" expr expr-list-rep "]"                lists
    | expr arg-rep                              function application
    | expr infix-op expr                        infix ops
    | "if" expr "then" expr "else" expr         if-else statements
    | "match" expr "with" pattern-matching      match statements
    | "fun" pattern "->" expr                   lambdas
    | "let" rec-opt let-binding "in" expr       let bindings

expr-tuple-rep ::= "," expr | "," expr expr-tuple-rep

expr-list-rep ::= "" | ";" expr expr-list-rep

arg-rep ::= expr | expr arg-rep

rec-opt ::= "" | "rec"

infix-op ::= "+" | "*" | "=" | "<" | "||" | "&&"

constant ::= integer-literal | "false" | "true" | "()" | "[]"

pattern-matching ::=
    | "|" pattern "->" expr
    | "|" pattern "->" expr pattern-matching

pattern ::=
    | value-name
    | "_"
    | constant
    | "(" pattern ")"
    | pattern pattern-tuple-rep
    | "[" pattern pattern-list-rep "]"
    | pattern "::" pattern

pattern-tuple-rep ::= "," pattern | "," pattern pattern-tuple-rep

pattern-list-rep ::= "" | ";" pattern pattern-list-rep

let-binding ::= pattern "=" expr
